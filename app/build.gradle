apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.jakewharton.butterknife'

def AAVersion = '4.8.0'
def SupportLibVersion = '28.0.0'

// Signing configuration from environment or GitHub secrets
def signingKeystorePassword = System.getenv('KEYSTORE_PASSWORD') ?: ''
def signingKeyAlias = System.getenv('KEY_ALIAS') ?: 'autojs'
def signingKeyPassword = System.getenv('KEY_PASSWORD') ?: signingKeystorePassword
def signingKeystoreFile = System.getenv('KEYSTORE_FILE') ?: ''

configurations.all {
    resolutionStrategy {
        force "com.android.support:appcompat-v7:${SupportLibVersion}"
        force "com.android.support:support-v4:${SupportLibVersion}"
    }
}

android {
    compileSdkVersion 31
    buildToolsVersion versions.buildTool
    
    // Signing configurations
    signingConfigs {
        debug {
            storeFile file('debug/autojs-debug.jks')
            storePassword 'autojs123'
            keyPassword 'autojs123'
            keyAlias 'autojs-debug'
        }
        release {
            // Use keystore from file path (set by CI) or fallback to debug keystore
            def keystoreFile = signingKeystoreFile && new File(signingKeystoreFile).exists() 
                ? file(signingKeystoreFile) 
                : file('debug/autojs-debug.jks')
            storeFile keystoreFile
            storePassword signingKeystorePassword ?: 'autojs123'
            keyPassword signingKeyPassword ?: 'autojs123'
            keyAlias signingKeyAlias ?: 'autojs-debug'
        }
    }
    
    defaultConfig {
        applicationId "org.autojs.autojs"
        minSdkVersion versions.mini
        targetSdkVersion versions.target
        versionCode versions.appVersionCode
        versionName versions.appVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        debug {
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        release {
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // Use release signing if keystore is configured, otherwise use debug signing
            if (signingKeystorePassword && signingKeystoreFile && file(signingKeystoreFile).exists()) {
                signingConfig signingConfigs.release
            } else {
                // Fallback to debug signing for CI builds without release keystore
                signingConfig signingConfigs.debug
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        encoding = 'utf-8'
    }
    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.1'
    }

    flavorDimensions "channel"
    splits {

        // Configures multiple APKs based on ABI.
        abi {

            // Enables building multiple APKs per ABI.
            enable true

            // By default all ABIs are included, so use reset() and include to specify that we only
            // want APKs for x86 and x86_64.

            // Resets the list of ABIs that Gradle should create APKs for to none.
            reset()

            // Specifies a list of ABIs that Gradle should create APKs for.
            include "x86", "armeabi-v7a"

            // Specifies that we do not want to also generate a universal APK that includes all ABIs.
            universalApk false
        }
    }
    productFlavors {
        common {
            buildConfigField "String", "CHANNEL", '"common"'
        }
        coolapk {
            buildConfigField "String", "CHANNEL", '"coolapk"'
        }
    }

}




repositories {
    flatDir {
        dirs 'libs'
    }
    google()
    mavenCentral()
    maven { url "https://maven.aliyun.com/repository/central" }
    maven { url "https://maven.aliyun.com/repository/google" }
    maven { url "https://maven.aliyun.com/repository/public" }
}

dependencies {
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.1-alpha01', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation "junit:junit:$junit_version"
    // Kotlin
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.1"
    // Android Annotations
    kapt "org.androidannotations:androidannotations:$AAVersion"
    implementation "org.androidannotations:androidannotations-api:$AAVersion"
    // ButterKnife
    implementation('com.jakewharton:butterknife:10.2.3', {
        exclude group: 'com.android.support'
    })
    kapt 'com.jakewharton:butterknife-compiler:10.2.3'
    // Android supports
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation "com.google.android.material:material:$material_version"
    implementation 'androidx.multidex:multidex:2.0.1'
    // Personal libraries
    implementation 'com.github.hyb1996:MutableTheme:1.0.0'
    // Material Dialogs
    implementation('com.afollestad.material-dialogs:core:0.9.2.3', {
        exclude group: 'com.android.support'
    })
    // Common Markdown
    implementation 'com.github.atlassian:commonmark-java:commonmark-parent-0.9.0'
    // Android issue reporter (a github issue reporter)
    implementation('com.heinrichreimersoftware:android-issue-reporter:1.3.1', {
        exclude group: 'com.afollestad.material-dialogs'
        exclude group: 'com.android.support'
        // Force compatible version of expandable-layout
        exclude group: 'com.github.aakira', module: 'expandable-layout'
    })
    // Add compatible version of expandable-layout directly
    implementation('com.github.aakira:expandable-layout:1.4.3@aar') {
        transitive = true
    }
    //MultiLevelListView
    implementation 'com.github.hyb1996:android-multi-level-listview:1.1'
    //Licenses Dialog
    implementation 'de.psdev.licensesdialog:licensesdialog:1.8.1'
    //Expandable RecyclerView
    implementation 'com.bignerdranch.android:expandablerecyclerview:3.0.0-RC1'
    //FlexibleDivider
    implementation 'com.yqritc:recyclerview-flexibledivider:1.4.0'
    //???
    implementation 'com.wang.avi:library:2.1.3'
    //Commons-lang
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    //Commons-io
    implementation 'commons-io:commons-io:2.11.0'    //Expandable RecyclerView
    implementation 'com.thoughtbot:expandablerecyclerview:1.3'
    // ApkBuilder - use local module
    implementation project(':apkbuilder')
    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:2.2.21"
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'
    //Glide
    implementation('com.github.bumptech.glide:glide:4.12.0', {
        exclude group: 'com.android.support'
    })
    kapt 'com.github.bumptech.glide:compiler:4.15.1'
    //joda time
    implementation 'joda-time:joda-time:2.12.5'
    // Tasker Plugin - use compatible version
    implementation('com.twofortyfouram:android-plugin-client-sdk-for-locale:4.0.3') {
        // Exclude problematic transitive dependencies to avoid version conflicts
        exclude module: 'android-annotation'
        exclude module: 'android-assertion'
    }
    // Flurry
    implementation 'com.flurry.android:analytics:7.0.0@aar'
    // Bugly
    implementation 'com.tencent.bugly:crashreport:4.0.4'
    // MaterialDialogCommon
    implementation('com.afollestad.material-dialogs:commons:0.9.6.0', {
        exclude group: 'com.android.support'
    })
    // Android job
    implementation 'com.evernote:android-job:1.4.2'
    // LeakCanary 2.x - upgraded for Android 12+ compatibility
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.12'
    implementation project(':automator')
    implementation project(':common')
    implementation project(':autojs')
}
